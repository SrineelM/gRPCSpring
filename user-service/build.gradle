plugins {
    id 'org.springframework.boot'
}

description = 'User Service - Handles user registration, authentication, and profile management'

dependencies {
    // gRPC dependencies with correct transitive dependencies
    implementation platform("io.grpc:grpc-bom:${grpcVersion}")
    implementation "io.grpc:grpc-netty-shaded"
    implementation "io.micrometer:micrometer-registry-prometheus"

    // Spring Boot starters
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"

    // User module specific
    implementation 'org.passay:passay:1.6.4'
    implementation 'commons-validator:commons-validator:1.8.0'

    // gRPC Spring Boot starter
    implementation "net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE"
    implementation "net.devh:grpc-client-spring-boot-starter"

    // JWT (using dependency management for versions)
    implementation "io.jsonwebtoken:jjwt-api"
    runtimeOnly "io.jsonwebtoken:jjwt-impl"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson"

    // Project shared code
    implementation project(':grpc-common')

    // Development runtime
    runtimeOnly 'com.h2database:h2'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'com.icegreen:greenmail-junit5:2.0.1' // for email unit tests
    testImplementation 'org.testcontainers:postgresql'

    // Lombok
    implementation "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // Resilience4j for circuit breaker, retry, timelimiter annotations
    implementation "io.github.resilience4j:resilience4j-circuitbreaker:2.2.0"
    implementation "io.github.resilience4j:resilience4j-retry:2.2.0"
    implementation "io.github.resilience4j:resilience4j-timelimiter:2.2.0"
    implementation "io.github.resilience4j:resilience4j-annotations:2.2.0"
}

springBoot {
    buildInfo()
    mainClass = 'com.poc.grpc.user.UserServiceApplication'
}

tasks.register('buildDockerImage', Exec) {
    dependsOn bootJar
    group = 'docker'
    description = 'Builds Docker image for user service'
    commandLine 'docker', 'build', '-t', "${project.name}:${version}", '.'
}

tasks.register('runLocal', JavaExec) {
    group = 'application'
    description = 'Run user service locally'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.poc.grpc.user.UserServiceApplication'
    args = ['--spring.profiles.active=local']
    jvmArgs = [
        '-Xmx512m'
    ]
}
